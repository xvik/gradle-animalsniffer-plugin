plugins {
    id 'java'
    id 'ru.vyarus.animalsniffer' version '1.6.0'
}

// just an example of custom configuration usage, it might be defailt 'signature' configuration, but it would affect
// animalsniffer check task
configurations.create('buildSignatures')

animalsnifferSignature {
//    debug = true
    // signature from project classes
    files sourceSets.main.output
    files configurations.compileClasspath
    signatures configurations.buildSignatures
    // by default project name
    outputName 'mySignature'
}

repositories { mavenCentral()}
dependencies {
    buildSignatures 'org.codehaus.mojo.signature:java16-sun:1.0@signature'

    implementation "org.codehaus.mojo:animal-sniffer-annotations:1.14"
    implementation 'org.slf4j:slf4j-api:1.7.25'
}

tasks.register('printSignature', ru.vyarus.gradle.plugin.animalsniffer.info.SignatureInfoTask) {
    signature = tasks.animalsnifferSignature.outputFiles
    depth = 2
}

tasks.build.dependsOn 'printSignature'