plugins {
    id 'com.gradle.build-scan' version '3.0'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'net.researchgate.release' version '2.8.1'
    id 'ru.vyarus.quality' version '4.3.0'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'ru.vyarus.java-lib' version '2.1.0'
    id 'ru.vyarus.github-info' version '1.2.0'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.7"
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '5.6.4'
}

repositories { mavenCentral(); gradlePluginPortal() }
dependencies {
    implementation "org.codehaus.mojo:animal-sniffer:1.18"

    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
}

group = 'ru.vyarus'
description = 'Gradle AnimalSniffer plugin'

github {
    user 'xvik'
    license 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['gradle', 'animalsniffer']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

gradlePlugin {
    plugins {
        animalsnifferPlugin {
            id = 'ru.vyarus.animalsniffer'
            displayName = project.description
            implementationClass = 'ru.vyarus.gradle.plugin.animalsniffer.AnimalSnifferPlugin'
        }
    }
}
pluginBundle {
    description = 'Gradle AnimalSniffer plugin for Java projects'
    tags = ['animalsniffer']

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload, publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed', 'standard_error'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishOnFailure()
}